plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id 'maven-publish'
    id 'signing'
}

ext {
    kotlinVersion = '1.3.21'
    junitVersion = '5.4.0'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: "$kotlinVersion"
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: "$kotlinVersion"

    testCompile group: 'org.mongodb', name: 'bson', version: '4.2.3'

    testCompile group: 'com.natpryce', name: 'hamkrest', version: '1.6.0.0'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "$junitVersion"
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "$junitVersion"
}

allprojects {
    version = VERSION_NAME
    group = GROUP

    repositories {
        mavenCentral()
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'map4k'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Map4K'
                description = 'Simple annotation free mapping for Kotlin.'
                url = 'https://github.com/zerocode/map4k'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'zerocode'
                        name = 'zerocode'
                        email = 'ioloop@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:zerocode/map4k.git'
                    developerConnection = 'scm:git@github.com:zerocode/map4k.git'
                    url = 'https://github.com/zerocode/map4k'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = RELEASE_REPOSITORY_URL
            def snapshotsRepoUrl = SNAPSHOT_REPOSITORY_URL
            url = isReleaseBuild() ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = NEXUS_USERNAME
                password = NEXUS_PASSWORD
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == true
}